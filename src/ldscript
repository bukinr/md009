MEMORY
{
	/*
	 * top 0x8000 bytes is reserved:
	 * first 0x4000 is DTB
	 * top 0x4000 is the disk image
	 */
	flash   (rx)  : ORIGIN = 0x00040000, LENGTH = 1M - 0x40000 - 0x8000
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 16K /* boot loader */
	sram1   (rwx) : ORIGIN = 0x20004000, LENGTH = 48K /* malloc */
	sram2   (rwx) : ORIGIN = 0x20010000, LENGTH = 64K /* bsdlib fixed */
	sram3   (rwx) : ORIGIN = 0x20020000, LENGTH = 64K /* this app */
	sram4   (rwx) : ORIGIN = 0x20030000, LENGTH = 64K /* malloc */
}

ENTRY(__start)
SECTIONS
{
	. = 0x40000;
	.start . : {
		*start.o(.text);
		*jump.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.sysinit : {
		__sysinit_start = ABSOLUTE(.);
		*(.sysinit)
		__sysinit_end = ABSOLUTE(.);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram3 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram3
}
