MEMORY
{
	flash   (rx)  : ORIGIN = 0x00040000, LENGTH = 1M - 0x40000
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 24K /* boot loader */
	sram1   (rwx) : ORIGIN = 0x20006000, LENGTH = 40K /* malloc */
	sram2   (rwx) : ORIGIN = 0x20010000, LENGTH = 64K /* bsdlib fixed */
	sram3   (rwx) : ORIGIN = 0x20020000, LENGTH = 64K /* this app */
	sram4   (rwx) : ORIGIN = 0x20030000, LENGTH = 64K /* malloc */
}

ENTRY(__start)
SECTIONS
{
	. = 0x40000;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram3 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram3
}
